# Querry all applications and settings to contextualized preferences 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX c4a:  <http://rbmm.org/schemas/cloud4all/0.1/>
SELECT DISTINCT
	?type
	(str(?c) as ?contextID)
	(str(?an) as ?appName)
	(str(?ai) as ?appID)	
	(str(?aa) as ?appActive)	
	(str(?service_name) as ?serviceName)
	(str(?service_priority) as ?serviceprio)
	(str(?prop_name) as ?propName)
	(str(?prop_value) as ?propValue)
	(str(?fromServ) as ?fromServiceName)
	(str(?fromVar) as ?fromVariableName)
	(str(?toServ) as ?toServiceName)
	(str(?toVar) as ?toVariableName)
WHERE
{
	?if rdf:type c4a:InferredConfiguration.
	?if c4a:id ?c.
	?if c4a:refersTo ?app.
	?app c4a:name ?an.
	?app c4a:id ?ai.
	?app c4a:isActive ?aa.
	?app c4a:settings ?serviceSetting.
	?serviceSetting rdf:type ?type.
	?serviceSetting c4a:serviceName ?service_name.
	?serviceSetting c4a:priority ?service_priority.
	?serviceSetting c4a:serviceInput ?prop.
	?prop c4a:name ?prop_name.
	?prop c4a:value ?prop_value.
	OPTIONAL{?serviceSetting c4a:hasMetadata ?metadata}.
	OPTIONAL{?metadata c4a:fromServiceName ?fromServ}.
	OPTIONAL{?metadata c4a:fromVariableName ?fromVar}.
	OPTIONAL{?metadata c4a:toServiceName ?toServ}.
	OPTIONAL{?metadata c4a:toVariableName ?toVar}.
} ORDER BY DESC(?service_priority)	